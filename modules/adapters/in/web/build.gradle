plugins {
    id 'org.springframework.boot' version '3.4.1'
    id 'org.asciidoctor.jvm.convert' version '4.0.2'
}

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    snippetsDir = file('build/generated-snippets')
}

dependencies {
    implementation project(":modules:application")
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.data:spring-data-commons'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

test {
    useJUnitPlatform()
    outputs.upToDateWhen { false }
}

asciidoctor {

    if (System.getProperty("deleteSnippets") || System.getenv("DELETE_SNIPPETS")) {
        delete file("./build/generated-snippets")
    }

    inputs.dir snippetsDir
    sourceDir = file("src/docs/asciidoc/")
    sources {
        include 'index.adoc'
    }
    resources {
        from(sourceDir) {
            include('images/**', 'fonts/**', 'styles/**', 'themes/**')
        }
    }
    configurations 'asciidoctorExt'

    attributes = ['doctype'           : 'book',
                  'description'       : '아카이브 오브 옹알 유니버스 API 문서입니다.',
                  'icons'             : 'font',
                  'source-highlighter': 'highlightjs',
                  'highlightjs-theme' : true,
                  'toc'               : 'left',
                  'toclevels'         : '3',
                  'toc-title'         : '목차',
                  'imagesdir'         : './images',
                  'linkcss'           : true,
                  'stylesdir'         : './styles',
                  'stylesheet'        : 'stylesheet.css',
                  'favicon'           : './images/favicon.ico']

    dependsOn test
}

jar {
    dependsOn asciidoctor
}

bootJar.enabled = false